enum INTERACTION_TYPE {
	CONFIRM,
	INPUT_CONFIRM
}
enum INTERACTION_STATUS {
	OFF,
	HOLD,
	PRESSED,
	RELEASED
}


function INTERACTION_MAPPINGS() constructor
{
	MAPPINGS = [];

    static SET_MAPPING = function(MAPPING_NO, MAPPING)
    {
		MAPPINGS[MAPPING_NO] = MAPPING;
		return MAPPING;
    }
}

function INTERACTION_MAPPING(_KEYS) constructor
{
	KEYS = _KEYS;

	static GET_STATUS = function()
	{
		var DISPATCHING_STATUS = INTERACTION_STATUS.OFF;
		for (var INDEX = 0; INDEX < array_length(KEYS); INDEX++)
		{
			var KEY = KEYS[INDEX];
			if keyboard_check_pressed(KEY.KEYCODE)
				&& DISPATCHING_STATUS != INTERACTION_STATUS.HOLD
			{
				DISPATCHING_STATUS = INTERACTION_STATUS.PRESSED;
			}
			else if keyboard_check_released(KEY.KEYCODE)
				&& DISPATCHING_STATUS != INTERACTION_STATUS.HOLD
				&& DISPATCHING_STATUS != INTERACTION_STATUS.PRESSED
			{
				DISPATCHING_STATUS = INTERACTION_STATUS.RELEASED;
			}
			else if keyboard_check(KEY.KEYCODE)
			{
				DISPATCHING_STATUS = INTERACTION_STATUS.HOLD;
			}
		}
		return DISPATCHING_STATUS;
	}

    static CLEAR_KEYS = function()
    {
		for (var INDEX = 0; INDEX < array_length(KEYS); INDEX++)
		{
			var KEY = KEYS[INDEX];
			keyboard_clear(KEY);
		}
    }
}
function INTERACTION_MAPPING_KEY(_KEYCODE, _ALIAS) constructor
{
	KEYCODE = _KEYCODE;
	ALIAS = _ALIAS;

	static toString = function()
	{
		return ALIAS;
	}
}


var INTERACTION_KEYS = {
	SHIFT: new INTERACTION_MAPPING_KEY(vk_shift, "shift"),
	ENTER: new INTERACTION_MAPPING_KEY(vk_enter, "enter"),
	// Z: new INTERACTION_MAPPING_KEY(ord("Z"), "Z"),
	X: new INTERACTION_MAPPING_KEY(ord("X"), "X")
};
global.INTERACTION_MAPPINGS = new INTERACTION_MAPPINGS();
global.INTERACTION_MAPPINGS.SET_MAPPING(
	INTERACTION_TYPE.CONFIRM,
	new INTERACTION_MAPPING([INTERACTION_KEYS.SHIFT, INTERACTION_KEYS.X])
);
global.INTERACTION_MAPPINGS.SET_MAPPING(
	INTERACTION_TYPE.INPUT_CONFIRM,
	new INTERACTION_MAPPING([INTERACTION_KEYS.ENTER]) // , INTERACTION_KEYS.Z])
);

global.INTERACTION_STATUS = array_create(array_length(global.INTERACTION_MAPPINGS.MAPPINGS));


function array_join(array, sep = " ")
{
	var str = string(array[0]);
	if (array_length(array) > 1)
	{
		for (var i = 1; i < array_length(array); i++)
		{
			str = str + sep + string(array[i]);
		}
	}
	return str;
}

function ACTION_INTERACTIONS_KEYS(INTERACTION)
{
	return global.INTERACTION_MAPPINGS.MAPPINGS[INTERACTION].KEYS;
}
function ACTION_INTERACTIONS_ALIAS(INTERACTION, TYPE = -1)
{
	var KEYS = ACTION_INTERACTIONS_KEYS(INTERACTION);
	if (TYPE == -1) 
	{
		return array_join(KEYS, "/");
	}
	return KEYS[TYPE].ALIAS;
}

function ACTION_INTERACTIONS_UPDATE()
{
	for (var INDEX = 0; INDEX < array_length(global.INTERACTION_MAPPINGS.MAPPINGS); INDEX++)
	{
		var MAPPING = global.INTERACTION_MAPPINGS.MAPPINGS[INDEX];
		global.INTERACTION_STATUS[INDEX] = MAPPING.GET_STATUS();
	}
}

function ACTION_INTERACTIONS_CHECK(INTERACTION)
{
	return global.INTERACTION_STATUS[INTERACTION] == INTERACTION_STATUS.HOLD;
}
function ACTION_INTERACTIONS_CHECK_PRESSED(INTERACTION)
{
	return global.INTERACTION_STATUS[INTERACTION] == INTERACTION_STATUS.PRESSED;
}
function ACTION_INTERACTIONS_CHECK_RELEASED(INTERACTION)
{
	return global.INTERACTION_STATUS[INTERACTION] == INTERACTION_STATUS.RELEASED;
}

function INTERACTIONS_CHECK(INTERACTION) {
	return ACTION_INTERACTIONS_CHECK(INTERACTION);
}
function INTERACTIONS_CHECK_PRESSED(INTERACTION) {
	return ACTION_INTERACTIONS_CHECK_PRESSED(INTERACTION);
}
function INTERACTIONS_CHECK_RELEASED(INTERACTION) {
	return ACTION_INTERACTIONS_CHECK_RELEASED(INTERACTION);
}

function ACTION_INTERACTIONS_CLEAR(INTERACTION)
{
	var MAPPING = global.INTERACTION_MAPPINGS.MAPPINGS[INTERACTION];
	global.INTERACTION_STATUS[INTERACTION] = 0;
	MAPPING.CLEAR_KEYS();
}
function ACTION_INTERACTIONS_CLEAR_ALL()
{
	for (var INDEX = 0; INDEX < array_length(global.INTERACTION_MAPPINGS.MAPPINGS); INDEX++)
	{
		ACTION_INTERACTIONS_CLEAR(INDEX);
	}
}

function INTERACTIONS_CLEAR(INTERACTION) {
	return ACTION_INTERACTIONS_CLEAR(INTERACTION);
}
function INTERACTIONS_CLEAR_ALL() {
	return ACTION_INTERACTIONS_CLEAR_ALL();
}